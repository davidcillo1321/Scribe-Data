# tool: scribe-data
# All Arabic (Q13955) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?genitivePlural
  ?accusativePlural
  ?genitiveIndefiniteDual
  ?genitiveDualConstruct
  ?accusativeIndefiniteDual
  ?accusativeDualConstruct

WHERE {
  ?lexeme dct:language wd:Q13955 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativePluralForm .
    ?accusativePluralForm ontolex:representation ?accusativePlural ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveIndefiniteDualForm .
    ?genitiveIndefiniteDualForm ontolex:representation ?genitiveIndefiniteDual ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q53997857, wd:Q110022 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveDualConstructForm .
    ?genitiveDualConstructForm ontolex:representation ?genitiveDualConstruct ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q110022, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeIndefiniteDualForm .
    ?accusativeIndefiniteDualForm ontolex:representation ?accusativeIndefiniteDual ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q53997857, wd:Q110022 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeDualConstructForm .
    ?accusativeDualConstructForm ontolex:representation ?accusativeDualConstruct ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q110022, wd:Q1641446 .
  }
}

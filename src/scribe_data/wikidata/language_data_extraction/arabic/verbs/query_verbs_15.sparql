# tool: scribe-data
# All Arabic (Q13955) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?gerund
  ?activeParticiple
  ?masculinePastSingular
  ?activePastSecondPersonDual

WHERE {
  ?lexeme dct:language wd:Q13955 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?gerundForm .
    ?gerundForm ontolex:representation ?gerund ;
      wikibase:grammaticalFeature wd:Q1923028 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activeParticipleForm .
    ?activeParticipleForm ontolex:representation ?activeParticiple ;
      wikibase:grammaticalFeature wd:Q72249355 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePastSingularForm .
    ?masculinePastSingularForm ontolex:representation ?masculinePastSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q1994301, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePastSecondPersonDualForm .
    ?activePastSecondPersonDualForm ontolex:representation ?activePastSecondPersonDual ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q1994301, wd:Q51929049, wd:Q110022 .
  }
}
